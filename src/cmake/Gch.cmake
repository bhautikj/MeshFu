# Based on a script from
# http://www.cmake.org/Bug/view.php?id=1260#c21475
MACRO(ADD_PCH_INCLUDE HEADER SOURCES)
  SET_SOURCE_FILES_PROPERTIES(
    ${${SOURCES}}
    PROPERTIES
    COMPILE_FLAGS "-Winvalid-pch -include ${HEADER}")
ENDMACRO(ADD_PCH_INCLUDE)

MACRO (ADD_PCH_RULE HEADER SOURCES)
  # FIXME this variable is duplicated because I don't remember cmake scope rules
  ADD_PCH_INCLUDE(${HEADER} ${SOURCES})
  SET(PRECOMPILED_HEADER "${HEADER}.gch")
  LIST(APPEND ${SOURCES} ${PRECOMPILED_HEADER})

  SET(CURRENT_INC_FLAGS)
  GET_DIRECTORY_PROPERTY(_dirs INCLUDE_DIRECTORIES)
  FOREACH (_dir ${_dirs})
    STRING(REGEX REPLACE ".framework$" ".framework/Headers" _dir ${_dir})
    LIST(APPEND CURRENT_INC_FLAGS "-I${_dir}")
  ENDFOREACH(_dir ${_dirs})

  GET_DIRECTORY_PROPERTY(DEFS DEFINITIONS)

  STRING(TOUPPER ${CMAKE_BUILD_TYPE} BUILD_TYPE)
  IF(QT4_FOUND AND ${BUILD_TYPE} STREQUAL "DEBUG")
    LIST(APPEND DEFS -DQT_DEBUG)
  ELSE(QT4_FOUND AND ${BUILD_TYPE} STREQUAL "DEBUG")
    LIST(APPEND DEFS -DQT_NO_DEBUG)
  ENDIF(QT4_FOUND AND ${BUILD_TYPE} STREQUAL "DEBUG")

  SET(PCH_FLAGS
    ${DEFS} ${CMAKE_CXX_FLAGS} ${CMAKE_CXX_FLAGS_${BUILD_TYPE}} ${CURRENT_INC_FLAGS})
  SEPARATE_ARGUMENTS(PCH_FLAGS)
  STRING(REPLACE ";" " " PRETTY_PCH_FLAGS "${PCH_FLAGS}")

  ADD_CUSTOM_COMMAND(OUTPUT ${PRECOMPILED_HEADER}
    COMMAND rm -f ${PRECOMPILED_HEADER}
    COMMAND ${CMAKE_CXX_COMPILER}
                  ${PCH_FLAGS}
                  -c ${CMAKE_CURRENT_SOURCE_DIR}/${HEADER}
                  -o ${CMAKE_CURRENT_BINARY_DIR}/${PRECOMPILED_HEADER}
    DEPENDS ${HEADER})
ENDMACRO(ADD_PCH_RULE HEADER SOURCES)


